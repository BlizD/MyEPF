Функция ПолучитьТаблицуВерсийХранилища(Приложение, БазаАдрес, БазаПользователь, БазаПароль, 
									ХранилищеАдрес, ХранилищеПользователь, ХранилищеПароль,
									НачинаяСВерсии = Неопределено, ЗаканчиваяНаВерсии = Неопределено) Экспорт
	
	КаталогВременныхФайлов = КаталогВременныхФайлов();		
	ТабличныйДокументВерсий = КаталогВременныхФайлов + "vhistory1.mxl";
	//ТабличныйДокументВерсий = "D:\Test\vhistory1.mxl";
	
	
	ТекстКоманды = СоздатьКоманду(Приложение);
	ДобавитьВКомандуКлючЗначение(ТекстКоманды, "/F", БазаАдрес);
	ДобавитьВКомандуКлючЗначение(ТекстКоманды, "/N", БазаПользователь);
	ДобавитьВКомандуКлючЗначение(ТекстКоманды, "/P", БазаПароль);
	ДобавитьВКомандуКлючЗначение(ТекстКоманды, "/ConfigurationRepositoryF", ХранилищеАдрес);
	ДобавитьВКомандуКлючЗначение(ТекстКоманды, "/ConfigurationRepositoryN", ХранилищеПользователь);
	ДобавитьВКомандуКлючЗначение(ТекстКоманды, "/ConfigurationRepositoryP", ХранилищеПароль);
	ДобавитьВКомандуКлючЗначение(ТекстКоманды, "/ConfigurationRepositoryReport", ТабличныйДокументВерсий);	
	Если ЗначениеЗаполнено(НачинаяСВерсии) Тогда
		ДобавитьВКомандуКлючЗначение(ТекстКоманды, "-NBegin", Формат(НачинаяСВерсии, "ЧН=; ЧГ=0")); 
	КонецЕсли;
	Если ЗначениеЗаполнено(ЗаканчиваяНаВерсии) Тогда
		ДобавитьВКомандуКлючЗначение(ТекстКоманды, "-NEnd", Формат(ЗаканчиваяНаВерсии, "ЧН=; ЧГ=0")); 
	КонецЕсли;
	//+ #67 Иванов А.Б. 2016-10-04
	//ДобавитьВКомандуКлючЗначение(ТекстКоманды, "-GroupByComment"); 
	//- #67 Иванов А.Б. 2016-10-04
	
	
	КодВозврата = ВыполнитьКоманду(ТекстКоманды);	
	//Сообщить(""+ТекстКоманды);
	Если КодВозврата <> 0 Тогда
		ОписаниеОшибки = "При получении таблицы версий хранилища произошла неизвестная ошибка";
		//Сообщить("Описание ошибки "+ ОписаниеОшибки());
		ВызватьИсключение ИсключениеОшибкаПриВыполненииКоманды(ОписаниеОшибки, ТекстКоманды);
	КонецЕсли;
	
	ТабДок = Новый ТабличныйДокумент();
	ТабДок.Прочитать(ТабличныйДокументВерсий);
	ТаблицаВерсий = ПолучитьТаблицуВерсийИзТабличногоДокумента(ТабДок);
	
	УдалитьФайлы(ТабличныйДокументВерсий);
	
	РезультатФункции = Новый Структура();
	РезультатФункции.Вставить("ТаблицаВерсий",ТаблицаВерсий);
	РезультатФункции.Вставить("ТабДокОтчет",ТабДок);
	Возврат РезультатФункции;
	
КонецФункции

Процедура ДобавитьВКомандуКлючЗначение(ТекстКоманды, Ключ, Значение = Неопределено)
	
	Если Значение = Неопределено Тогда
		ТекстКоманды = ТекстКоманды + " " + Ключ;
	Иначе	
		ТекстКоманды = ТекстКоманды + " " + Ключ + " """ + Экранировать(Значение) + """";
	КонецЕсли;
		
КонецПроцедуры

Функция Экранировать(Значение)
	
	Возврат СтрЗаменить(Значение, """", """""");	
	
КонецФункции

Функция СоздатьКоманду(Приложение)
	
	ТекстКоманды = """" + Приложение + """" + " DESIGNER ";
	Возврат ТекстКоманды;
	
КонецФункции

Функция ВыполнитьКоманду(ТекстКоманды)
	////+ Иванов А.Б. 2016-05-03
	//Сообщить(""+Формат(ТекущаяДата(),"ДФ=dd.MM.yyyy")
	//	+" - ПакетныйРежим.ВыполнитьКоманду ТекстКоманды ["+ТекстКоманды+"]");
	////- Иванов А.Б. 2016-05-03
		
	КодВозврата = Неопределено;
	ЗапуститьПриложение(ТекстКоманды,, Истина, КодВозврата);
	Возврат КодВозврата;
	
КонецФункции

Функция ИсключениеОшибкаПриВыполненииКоманды(ОписаниеОшибки, ТекстКоманды)
	
	Возврат ОписаниеОшибки + "(" + ТекстКоманды + ")";	
	
КонецФункции

Функция ПолучитьТаблицуВерсийИзТабличногоДокумента(ТабличныйДокумент)
		
	ТаблицаВерсий = Новый ТаблицаЗначений;
	ТаблицаВерсий.Колонки.Добавить("НомерВерсии", Новый ОписаниеТипов("Число"));
	ТаблицаВерсий.Колонки.Добавить("ИмяПользователя", Новый ОписаниеТипов("Строка"));
	ТаблицаВерсий.Колонки.Добавить("ДатаСоздания", Новый ОписаниеТипов("Дата"));
	ТаблицаВерсий.Колонки.Добавить("Комментарий", Новый ОписаниеТипов("Строка"));
	
	НачинаяСоСтроки = 1;
	Пока ТабличныйДокумент.ВысотаТаблицы >= НачинаяСоСтроки Цикл
		ОбластьПоиска = ТабличныйДокумент.Область(НачинаяСоСтроки,1,ТабличныйДокумент.ВысотаТаблицы,1);
		ОбластьРезультат = ТабличныйДокумент.НайтиТекст("Версия:",,ОбластьПоиска, Истина, Истина, Истина, Ложь); 		
		Если ОбластьРезультат <> Неопределено Тогда
			НоваяСтрока = ТаблицаВерсий.Добавить();
			НоваяСтрока.НомерВерсии = Число(ТабличныйДокумент.Область(ОбластьРезультат.Верх, 2).Текст);
			НоваяСтрока.ИмяПользователя = ТабличныйДокумент.Область(ОбластьРезультат.Верх + 1, 2).Текст;
			ДатаСоздания = ТабличныйДокумент.Область(ОбластьРезультат.Верх + 2, 2).Текст;
			ВремяСоздания = ТабличныйДокумент.Область(ОбластьРезультат.Верх + 3, 2).Текст;
			НоваяСтрока.ДатаСоздания = Дата(ДатаСоздания + " " + ВремяСоздания);
			НоваяСтрока.Комментарий = ТабличныйДокумент.Область(ОбластьРезультат.Верх + 4, 2).Текст;
			
			НачинаяСоСтроки = ОбластьРезультат.Верх + 5;
		Иначе
			Прервать;
	    КонецЕсли;
	КонецЦикла;
	
	Возврат ТаблицаВерсий;
		
КонецФункции
