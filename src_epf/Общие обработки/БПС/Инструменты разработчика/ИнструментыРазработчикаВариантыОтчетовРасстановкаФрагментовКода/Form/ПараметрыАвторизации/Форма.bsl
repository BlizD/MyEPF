#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры, "Пользователь, Пароль");
	Если НЕ ЗначениеЗаполнено(Пользователь) Тогда
		Пользователь = ИмяПользователя();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если ЗначениеЗаполнено(Пользователь) Тогда
		ТекущийЭлемент = Элементы.Пароль;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	Если НЕ РезультатВыбораСформирован Тогда
		ВернутьРезультатВыбора(Неопределено);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)
	Если НЕ ПользовательИБСуществует(Пользователь) Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Пользователь не найден.'"));
		Возврат;
	КонецЕсли;
	
	РезультатВыбора = Новый Структура("Пользователь, Пароль", Пользователь, Пароль);
	ВернутьРезультатВыбора(РезультатВыбора);
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтмена(Команда)
	ВернутьРезультатВыбора(Неопределено);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВернутьРезультатВыбора(РезультатВыбора)
	РезультатВыбораСформирован = Истина;
	ОповеститьОВыборе(РезультатВыбора);
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПользовательИБСуществует(Имя)
	Возврат (ПользователиИнформационнойБазы.НайтиПоИмени(Имя) <> Неопределено);
КонецФункции

#КонецОбласти