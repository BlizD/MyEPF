&НаКлиенте
Перем МассивЧастей,ВремяНачала,ПрерватьЗакачку,ЗагружаемыйФайл;
&НаКлиенте
Процедура ИмяФайлаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	#Если ВебКлиент Тогда
		НачатьПодключениеРасширенияРаботыСФайлами(Новый ОписаниеОповещения("ПослеПроверкиРасширения",ЭтаФорма));
	#Иначе
		ИмяФайлаВыборПродолжение(Новый Структура);	
	#КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПроверкиРасширения(Результат,ДополнительныеПараметры) Экспорт
	Если НЕ Результат Тогда
		Описание = Новый ОписаниеОповещения("ИмяФайлаВыборПродолжение",ЭтаФорма);
		НачатьУстановкуРасширенияРаботыСФайлами(Описание);
	Иначе
		ИмяФайлаВыборПродолжение(Новый Структура);	
	КонецЕсли;
КонецПроцедуры // ИмяФайлаВыборПродолжение()


&НаКлиенте
Процедура ИмяФайлаВыборПродолжение(ДополнительныеПараметры) Экспорт
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Оповещение = Новый ОписаниеОповещения("ВидеозаписиВыборФайлаПродолжение",ЭтаФорма);
	Диалог.Показать(Оповещение);
КонецПроцедуры // ИмяФайлаВыборПродолжение()

&НаКлиенте
Процедура ВидеозаписиВыборФайлаПродолжение(Результат,ДополнительныеПараметры) Экспорт
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ИмяФайла = Результат[0];
КонецПроцедуры // ВидеозаписиВыборФайлаПродолжение()

&НаКлиенте
Процедура Передать(Команда)
	Файл = Новый Файл(ИмяФайла);
	#Если ВебКлиент Тогда
	Описание = Новый ОписаниеОповещения("ПослеПолученияРазмераВебКлиент",ЭтаФорма,Новый Структура("Расширение",Файл.Расширение));	
	#Иначе
	Описание = Новый ОписаниеОповещения("ПослеПолученияРазмера",ЭтаФорма,Новый Структура("Расширение",Файл.Расширение));
	#КонецЕсли
	файл.НачатьПолучениеРазмера(Описание);
	ПрерватьЗакачку = Ложь;
	ВремяНачала = ТекущаяДата();
	СкачаноБайт = 0;
КонецПроцедуры

&НаКлиенте
Процедура ПослеПолученияРазмераВебКлиент(Результат,ДополнительныеПараметры) Экспорт
	ТекХэшФайла = ИмяФайла+СтрЗаменить(Результат,Символы.НПП,"");//на клиенте MD5 недоступен
	ДополнительныеПараметры.Вставить("Хэш",ТекХэшФайла);
	 ФайловыеПотоки.НачатьОткрытиеДляЧтения(Новый ОписаниеОповещения("ПослеСозданияДвоичныхДанных",ЭтаФорма,ДополнительныеПараметры),ИмяФайла);
	//НачатьСозданиеДвоичныхДанныхИзФайла(Новый ОписаниеОповещения("ПослеСозданияДвоичныхДанных",ЭтаФорма,ДополнительныеПараметры),ИмяФайла);
КонецПроцедуры // ПослеПолученияРазмера()

&НаКлиенте
Процедура ПослеСозданияДвоичныхДанных(Результат,ДополнительныеПараметры) Экспорт
	
	//поиск по списку значений
	ЗагружаемыйФайл = ПоискПоСпискуФайлов(ДополнительныеПараметры.Хэш,ДополнительныеПараметры.Расширение);
	ЧтениеДанных = Новый ЧтениеДанных(Результат);
	Описание = Новый ОписаниеОповещения("РезультатРазделенияНаЧасти",ЭтаФорма);
	ЧтениеДанных.НачатьРазделениеНаЧастиПо(Описание,1024*1024*5);//5 мбайт
КонецПроцедуры // ПослеПолученияРазмера()

&НаКлиенте
Процедура ПослеПолученияРазмера(Результат,ДополнительныеПараметры) Экспорт
	ТекХэшФайла = ИмяФайла+СтрЗаменить(Результат,Символы.НПП,"");//на клиенте MD5 недоступен
	//поиск по списку значений
	ЗагружаемыйФайл = ПоискПоСпискуФайлов(ТекХэшФайла,ДополнительныеПараметры.Расширение);
	ЧтениеДанных = Новый ЧтениеДанных(ИмяФайла);
	Описание = Новый ОписаниеОповещения("РезультатРазделенияНаЧасти",ЭтаФорма);
	ЧтениеДанных.НачатьРазделениеНаЧастиПо(Описание,1024*1024*5);//5 мбайт
//	ЧтениеДанных.НачатьЗакрытие(Новый ОписаниеОповещения("ОкончаниеЗакрытияПотока",ЭтаФорма));
КонецПроцедуры // ПослеПолученияРазмера()

&НаСервере
Функция ПоискПоСпискуФайлов(Хэш,Расширение)
	Для Каждого ЭлементСписка из СписокФайлов Цикл
		Если ЭлементСписка.Представление = Хэш Тогда
			Возврат СписокФайлов.Индекс(ЭлементСписка);
		КонецЕсли;
	КонецЦикла;
	СтруктураСписка = Новый Структура("ИмяФайлаНаСервере,МассивЧастей",ПолучитьИмяВременногоФайла(Расширение),Новый Массив);
	Возврат СписокФайлов.Индекс(СписокФайлов.Добавить(СтруктураСписка,Хэш));
КонецФункции // ПоискПоСпискуФайлов()

&НаКлиенте
Процедура РезультатРазделенияНаЧасти(Результат,ДопДанные) Экспорт
	МассивЧастей	 = Результат;
	Элементы.Индикатор.МаксимальноеЗначение = МассивЧастей.Количество()-1;
	Индикатор = СписокФайлов[ЗагружаемыйФайл].Значение.МассивЧастей.Количество(); //тут хранится количество загруженных частей
	ПередатьЧастиФайла();
	ПодключитьОбработчикОжидания("ПередатьЧастиФайла",0.5,Истина);
	Элементы.Пауза.Доступность = Истина;
	Элементы.Передать.Доступность = Ложь;
КонецПроцедуры // РезультатРазделенияНаЧасти()

&НаКлиенте
Процедура ПередатьЧастиФайла() Экспорт
	Если Индикатор<=Элементы.Индикатор.МаксимальноеЗначение Тогда
		Описание = Новый ОписаниеОповещения("ПередачаЧастиПродолжение",ЭтаФорма);
		МассивЧастей[Индикатор].НачатьПолучениеДвоичныхДанных(Описание);
	Иначе
		ПоказатьОповещениеПользователя("Файл "+ИмяФайла+" успешно загружен!",,"Путь на сервере: "+СписокФайлов[ЗагружаемыйФайл].Значение.ИмяФайлаНаСервере);
		ОтключитьОбработчикОжидания("ПередатьЧастиФайла");
		Элементы.Передать.Доступность = Истина;
		Элементы.Пауза.Доступность = Ложь;

	КонецЕсли;
	
КонецПроцедуры // ПередатьЧастиФайла()

&НаКлиенте
Процедура ПередачаЧастиПродолжение(Данные,ДопДанные) Экспорт
	Адрес = ПоместитьВоВременноеХранилище(Данные);
	ПриемЧастиФайла(Адрес,ЗагружаемыйФайл);	
	ПрошлоСекунд = ТекущаяДата()-ВремяНачала;
	Если ПрошлоСекунд>0 Тогда
		СкоростьЗакачки = Формат(СкачаноБайт/ПрошлоСекунд/1000,"ЧДЦ=")+" КБ/сек";
	КонецЕсли;
	ОбновитьОтображениеДанных();
	ОбработкаПрерыванияПользователя();
	ПодключитьОбработчикОжидания("ПередатьЧастиФайла",0.5,Истина);
	//	ПередатьЧастиФайла();//продолжаем передавать следующую часть
КонецПроцедуры // ПередачаЧастиПродолжение()

&НаСервере
Процедура ПриемЧастиФайла(Адрес,ИдЭлемента)
	Данные = ПолучитьИзВременногоХранилища(Адрес);
	ЭлементСписка = СписокФайлов[ИдЭлемента];
	ПотокДозаписи = ФайловыеПотоки.ОткрытьДляДописывания(ЭлементСписка.Значение.ИмяФайлаНаСервере,Данные.Размер());
	Буфер = Новый БуферДвоичныхДанных(Данные.Размер());
	Данные.ОткрытьПотокДляЧтения().Прочитать(Буфер,0,Данные.Размер());
	ПотокДозаписи.Записать(Буфер,0,Буфер.Размер);
	ПотокДозаписи.СброситьБуферы();
	СкачаноБайт = СкачаноБайт+Буфер.Размер;
	ЭлементСписка.Значение.МассивЧастей.Добавить(Индикатор);
	Индикатор = Индикатор+1;
КонецПроцедуры // ПриемЧастиФайла()


&НаКлиенте
Процедура Пауза(Команда)
	ОтключитьОбработчикОжидания("ПередатьЧастиФайла");
	Элементы.Передать.Доступность = Истина;
	Элементы.Пауза.Доступность = Ложь;
КонецПроцедуры

